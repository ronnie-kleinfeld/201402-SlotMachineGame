package com.gotchaslots.slots.mobile.ui.notifications.popup.invite4Unlock
{
	import com.gotchaslots.slots.mobile.data.Main;
	import com.gotchaslots.slots.mobile.data.application.locale.L10NHandler;
	import com.gotchaslots.slots.mobile.data.lobby.lobbyMachine.base.BaseLobbyMachineData;
	import com.gotchaslots.slots.mobile.ui.common.components.AndroidLoader;
	import com.gotchaslots.slots.mobile.ui.common.components.Spacer;
	import com.gotchaslots.slots.mobile.ui.notifications.NotificationsHandler;
	import com.gotchaslots.slots.mobile.ui.notifications.popup.base.BasePopup;
	import com.gotchaslots.slots.mobile.ui.notifications.popup.base.PopupSizeTypeEnum;
	import com.gotchaslots.slots.mobile.utils.xServices.social.SocialEvent;
	
	import flash.events.Event;
	
	public class Inviting4UnlockPopup extends BasePopup
	{
		// members
		private var _lobbyMachine:BaseLobbyMachineData;
		
		// properties
		protected override function get SizeType():String
		{
			return PopupSizeTypeEnum.SmallPopup;
		}
		
		protected override function get Title():String
		{
			return "Facebook";
		}
		
		protected override function get HasCloseButton():Boolean
		{
			return true;
		}
		
		// class
		public function Inviting4UnlockPopup(lobbyMachine:BaseLobbyMachineData, onClose:Function)
		{
			super(onClose);
			
			_lobbyMachine = lobbyMachine;
			
			Main.Instance.XServices.Social.Invite();
			Main.Instance.XServices.Social.addEventListener(SocialEvent.InviteFriendsDialogComplete, OnInviteFriendsDialogComplete);
			Main.Instance.XServices.Social.addEventListener(SocialEvent.Failed, OnFailed);
		}
		
		protected override function AddBody():void
		{
			AddBlueMessageTextField(W, Main.Instance.Application.L10N.ActiveLocale.GetString(L10NHandler.Inviting));
			AddComponent(new Spacer(6));
			var androidLoader:AndroidLoader = new AndroidLoader(100, 100);
			androidLoader.x = (W - androidLoader.width) / 2;
			AddComponent(androidLoader);
		}
		
		public override function Dispose():void
		{
			Main.Instance.XServices.Social.removeEventListener(SocialEvent.InviteFriendsDialogComplete, OnInviteFriendsDialogComplete);
			Main.Instance.XServices.Social.removeEventListener(SocialEvent.Failed, OnFailed);
			
			super.Dispose();
		}
		
		// events
		protected function OnInviteFriendsDialogComplete(event:SocialEvent):void
		{
			Main.Instance.XServices.Social.removeEventListener(SocialEvent.InviteFriendsDialogComplete, OnInviteFriendsDialogComplete);
			Main.Instance.XServices.Social.removeEventListener(SocialEvent.Failed, OnFailed);
			
			var newInviteesCount:int = 0;
			var ids:Vector.<String> = event.IDs;
			for (var i:int = 0; i < ids.length; i++)
			{
				var inviteeInvited:Boolean = false;
				for (var j:int = 0; j < Main.Instance.Session.Rare.Invitees.length; j++)
				{
					if (ids[i] == Main.Instance.Session.Rare.Invitees[j])
					{
						inviteeInvited = true;
						break;
					}
				}
				
				if (!inviteeInvited)
				{
					Main.Instance.Session.Rare.Invitees.push(ids[i]);
					newInviteesCount++;
				}
			}
			
			if (newInviteesCount > 0)
			{
				NotificationsHandler.Instance.ShowInvited4UnlockPopup(_lobbyMachine, newInviteesCount);
			}
			DoRemove();
		}
		protected function OnFailed(event:Event):void
		{
			Main.Instance.XServices.Social.removeEventListener(SocialEvent.InviteFriendsDialogComplete, OnInviteFriendsDialogComplete);
			Main.Instance.XServices.Social.removeEventListener(SocialEvent.Failed, OnFailed);
			
			DoRemove();
		}
		
		private function OnCancelClick(event:Event):void
		{
			DoRemove();
		}
	}
}